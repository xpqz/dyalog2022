:Namespace p21
    ⍝ https://contest.dyalog.com/?goto=P21

    ⎕FR ⎕PP ⎕IO←1287 34 1
    assert←{⍺←'assertion failure' ⋄ 0∊⍵:⍺ ⎕signal 8 ⋄ shy←0}

    runs ← {⍺↑⊃∨⌿⍺{⍺⍺↑(⍸⍣¯1)(¯1+⍺)+⍳⍵}/⍵}

    ∇ test_runs
      assert 0 0 1 1 1 1 0 0 0 0≡⎕←10 runs 1 2⍴3 4
      assert 0 0 1 1 1 1 0 1 1 1≡⎕←10 runs 2 2⍴3 4 8 6
      assert 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0≡⎕←15 runs 0 2⍴ 5 3
      assert 0 0 1 1 1 1 1 1 1 1 1 0 0 0 0≡⎕←15 runs 2 2⍴3 6 5 7
      assert 0 0 0 0 0 0 0 0 0 0≡⎕←10 runs 1 2⍴6 0
      assert ⍬≡⎕←0 runs 2 2⍴2 3 5 6
    ∇

    fill ← {
         _mkspc ← {
             offset ← ⍺-1                    
             shape ← ⍵⌊(⍺⍺-offset)⌊offset+⍵  ⍝ clamp shape to stay within operand
             ,(⊂offset)+⍳shape               ⍝ generate coordinates
         }
         m ← ⍺⍴0                   ⍝ Make empty result array
         specs ← (≢⍺)(↑,⍥⊂↓)⍤1⊢⍵   ⍝ Separate subspace spec into offset and shape
         spaces ← (⍺ _mkspc)/specs ⍝ Make clamped coordinate sets from the specs
         values ← (≢¨spaces)/⍳≢⍵   ⍝ Each subspace has the value of its index in the spec
         m[⊃,/spaces] ← values                  
         m
    }

    ∇ test_fill
      spaces←5 4⍴∊2/¨(⍳5),¨(⌽¯1+2×⍳5)
      expected←(9 9⍴(10/1),(7/2),1 1 2 3,(4/3),2 1 1 2 3 4,(2/4),3 2 1 1 2 3 4 5 4 3 2 1 1 2 3 4,(2/4),3 2 1 1 2 3,(4/3),2 1 1,(7/2),10/1)
      assert expected≡⎕←9 9 fill spaces
    ∇

    subspaces ← {
        extent ← {⊖(⍳≢⍴⍵){⊃0~⍨∪,+⌿⍤⍺⊢⍵}⍤0 99⊢⍵}
        loc ← (⍳≢0~⍨∪,⍵)∘.=⍵   ⍝ Boolean matrix where each major cell is the locations of the value in the arg.
        pos ← ↑,(1↑⍸)⍤¯1⊢loc   ⍝ pos holds first location of each element (in ravel order).
        shapes ← extent⍤¯1⊢loc ⍝ For the major cells in loc, find the extent of the block of 1s.
        pos,shapes
    }

    ∇ test_subspaces
      assert (3 2⍴5 4 2 2 10 1)≡⎕←subspaces 0 2 2 0 1 1 1 1 0 3                 
      assert (3 6⍴3,(5/1),2,(3/1),2 2,(4/1),3 3)≡⎕←subspaces ↑(⊢⍴⍨⊢,⊢)¨3 2 1
      assert (0 8⍴0)≡⎕←subspaces 5 4 3 2⍴0                           
      assert (5 4⍴3 4 1 2 1 4 2 2 4 3 2 3 4 1 2 2 1 1 3 3)≡⎕←subspaces ((3 3⍴5),(2 2⍴2)⍪1)⍪(2 2⍴4),2 3⍴3
    ∇
:EndNamespace